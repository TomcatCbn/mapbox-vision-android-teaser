apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply from: "../gradle/ktlint.gradle"
apply from: '../gradle/generate-token.gradle'

if (gradle.ext.has('BUILD_CORE_FROM_SOURCE')) {
    apply plugin: 'com.google.gms.google-services'
    apply plugin: 'com.google.firebase.crashlytics'
    apply plugin: 'com.google.firebase.firebase-perf'
}

android {
    buildToolsVersion build_tools_version
    compileSdkVersion compile_sdk_version

    defaultConfig {
        applicationId "com.mapbox.vision.recorder"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    splits {
        abi {
            enable true
            reset()
            include "arm64-v8a"
            universalApk false
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    signingConfigs {
        if (gradle.ext.has('SIGNING_CONFIG_RELEASE')) {
            release {
                storeFile file(gradle.ext.SIGNING_CONFIG_RELEASE)
                storePassword gradle.ext.SIGNING_CONFIG_RELEASE_PASSWORD
                keyAlias gradle.ext.SIGNING_CONFIG_RELEASE_ALIAS
                keyPassword gradle.ext.SIGNING_CONFIG_RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (gradle.ext.has('SIGNING_CONFIG_RELEASE')) {
                signingConfig signingConfigs.release
            }

            firebaseCrashlytics {
                nativeSymbolUploadEnabled true
                strippedNativeLibsDir 'build/intermediates/stripped_native_libs/release/out/lib/'
                unstrippedNativeLibsDir '../source/mapbox-vision/SDK/Platforms/Android/build/unstripped/release'
            }
        }
    }

    dexOptions {
        preDexLibraries true
        maxProcessCount 8
        javaMaxHeapSize "2g"
    }
}

dependencies {
    implementation project(':common')

    ktlint "com.pinterest:ktlint:$ktlint_version"
}
